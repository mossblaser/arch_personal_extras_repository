pkgname="qth_notify"
pkgdesc="Show notifications via Qth"
url="https://github.com/mossblaser/qth_notify"
pkgver="0.0.3"
pkgrel="1"
arch=('any')
license=('GPL3')

depends=('python-qth>=0.7.0' 'python-dbus-next' 'python-desktop-notify' 'python-setuptools' 'python>=3.11.0' 'python<3.12.0')
makedepends=(python-build python-installer python-wheel)

source=("https://pypi.io/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "${pkgname}.service")
sha256sums=('7b70ce29b7fc08d7176b96b837131462f77dd0ada39ee2309b5c6426e092e0de'
            'c001e0a62012cdb09c043c4bf9d5d55f61adea7adc3d01bf2fa55a46cabaf47c')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  
  install -Dm644 "$srcdir/${pkgname}.service" "$pkgdir/usr/lib/systemd/user/${pkgname}.service"
}
