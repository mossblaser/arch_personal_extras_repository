pkgname="qth_national_rail"
pkgdesc="show national rail service status in qth"
url="https://github.com/mossblaser/qth_national_rail"
pkgver="0.2.0"
pkgrel="0"
arch=('any')
license=('GPL2')

depends=('python-qth>=0.7.0' 'python-zeep' 'python-setuptools' 'python>=3.10.0' 'python<3.11.0')

source=("https://pypi.io/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "${pkgname}.env"
        "${pkgname//_/.}@.service")
md5sums=('9f4965dff069a95314ff73c8cb0ae902'
         'a383d591ac327a039ab4d8c2f5af95ae'
         '61919e9912ce5d6e693179aa393fb5b0')
backup=("etc/${pkgname}.env")

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  
  install -Dm644 "$srcdir/${pkgname//_/.}@.service" "$pkgdir/usr/lib/systemd/system/${pkgname//_/.}@.service"
  install -Dm644 "$srcdir/${pkgname}.env" "$pkgdir/etc/${pkgname}.env"
}
