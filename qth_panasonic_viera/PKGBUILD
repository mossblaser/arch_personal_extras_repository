pkgname="qth_panasonic_viera"
pkgdesc="Qth interface to certain panasonic TVs"
url="https://github.com/mossblaser/qth_pansonic_viera"
pkgver="0.1.0"
pkgrel="1"
arch=('any')
license=('GPL2')

depends=('python-qth>=0.6.0' 'python-panasonic-viera>=0.1.0' 'python-setuptools')

source=("https://pypi.io/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "${pkgname}.env"
        "${pkgname//_/.}.service")
md5sums=('20fa888b4b24f0329ce1c5cd1820d786'
         '6dfa17c52ca6c27b61d3b207d6d7dc81'
         '371dce26d545d26126e0e168a0566d51')
backup=("etc/${pkgname}.env")

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  
  install -Dm644 "$srcdir/${pkgname//_/.}.service" "$pkgdir/usr/lib/systemd/system/${pkgname//_/.}.service"
  install -Dm644 "$srcdir/${pkgname}.env" "$pkgdir/etc/${pkgname}.env"
}
