pkgname="qth_panasonic_viera"
pkgdesc="Qth interface to certain panasonic TVs"
url="https://github.com/mossblaser/qth_pansonic_viera"
pkgver="0.2.0"
pkgrel="4"
arch=('any')
license=('GPL2')

depends=('python-qth>=0.7.0' 'python-panasonic-viera>=0.1.0' 'python-setuptools' 'python>=3.13.0' 'python<3.14.0')

source=("https://pypi.io/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "${pkgname}.env"
        "${pkgname//_/.}.service")
md5sums=('ea714be7b9d714991cacc620cf29d661'
         '6dfa17c52ca6c27b61d3b207d6d7dc81'
         '9f21bf9e14f115e0b13e8113c8292402')
backup=("etc/${pkgname}.env")

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  
  install -Dm644 "$srcdir/${pkgname//_/.}.service" "$pkgdir/usr/lib/systemd/system/${pkgname//_/.}.service"
  install -Dm644 "$srcdir/${pkgname}.env" "$pkgdir/etc/${pkgname}.env"
}
