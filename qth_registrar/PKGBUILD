pkgname="qth_registrar"
pkgdesc="A Qth Registrar implementation"
url="https://github.com/mossblaser/qth_registrar"
pkgver="0.3.0"
pkgrel="3"
arch=('any')
license=('GPL2')

depends=('python-qth>=0.7.0' 'python-setuptools' 'python>=3.13.0' 'python<3.14.0')

source=("https://pypi.io/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"
        "${pkgname/_/.}.service")
md5sums=('1321078f27922805c96cd4379a8fe087'
         '81ba22d9ba7269931a764975dbf52b1b')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "$srcdir/${pkgname/_/.}.service" "$pkgdir/usr/lib/systemd/system/${pkgname/_/.}.service"
}
